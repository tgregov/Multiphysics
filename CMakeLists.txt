PROJECT(MULTIPHYSICS CXX)
CMAKE_MINIMUM_REQUIRED(VERSION 3.1)

# build type is "" by default in Linux
IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Debug" CACHE STRING "" FORCE)
ENDIF()

MESSAGE(STATUS "Build Type is " ${CMAKE_BUILD_TYPE})

# put all generated files in a single folder (required in windows for exe linked to dlls)
SET(LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin/ CACHE PATH "")
SET(EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin/ CACHE PATH "")
MARK_AS_ADVANCED(LIBRARY_OUTPUT_PATH EXECUTABLE_OUTPUT_PATH)

# enable C++11
SET(CMAKE_CXX_STANDARD 11) # newer way to set C++11 (requires cmake>3.1)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
#SET(CMAKE_CXX_FLAGS "-fopenmp") #enable openmp

# compiler options
IF(UNIX AND CMAKE_CXX_COMPILER_ID STREQUAL GNU)
    # allows gcc5/ubuntu to link against the c++ interface
    # (this does not work with mingw)
    ADD_DEFINITIONS(-D_GLIBCXX_USE_CXX11_ABI=0) # libgmsh is linked to libgcc4 (pbl with std::string)
ELSEIF(CMAKE_CXX_COMPILER_ID MATCHES MSVC)
    ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS -D_SCL_SECURE_NO_DEPRECATE -D_CRT_NONSTDC_NO_WARNINGS)
    ADD_DEFINITIONS(-D_USE_MATH_DEFINES) # for M_PI
ENDIF()

IF(APPLE)
    # on macOS, do not give priority to frameworks/apps
    SET(CMAKE_FIND_APPBUNDLE LAST)
    SET(CMAKE_FIND_FRAMEWORK LAST)
ENDIF()

# find gmsh-sdk
# gmsh.h
FIND_PATH(GMSH_INCLUDE_DIRS NAMES "gmsh.h")
MESSAGE(STATUS "GMSH_INCLUDE_DIRS=" ${GMSH_INCLUDE_DIRS})
if(NOT GMSH_INCLUDE_DIRS)
    MESSAGE(FATAL_ERROR "gmsh.h not found!")
ENDIF()
INCLUDE_DIRECTORIES(${GMSH_INCLUDE_DIRS})

# libgmsh.so
FIND_LIBRARY(GMSH_LIBRARIES gmsh)
MESSAGE(STATUS "GMSH_LIBRARIES=" ${GMSH_LIBRARIES})
IF(NOT GMSH_LIBRARIES)
    MESSAGE(FATAL_ERROR "gmsh library not found!")
ENDIF()

# gmsh.exe
FIND_PROGRAM(GMSH_EXECUTABLE gmsh)
MESSAGE(STATUS "GMSH_EXECUTABLE=" ${GMSH_EXECUTABLE})
IF(NOT GMSH_EXECUTABLE)
    MESSAGE(FATAL_ERROR "gmsh executable not found!")
ENDIF()

FIND_PATH(EIGEN_INCLUDE_DIRS NAMES "Eigen")
MESSAGE(STATUS "EIGEN_INCLUDE_DIRS=" ${EIGEN_INCLUDE_DIRS})
if(NOT EIGEN_INCLUDE_DIRS)
    MESSAGE(FATAL_ERROR "eigen not found!")
ENDIF()
INCLUDE_DIRECTORIES(${EIGEN_INCLUDE_DIRS})

FIND_PACKAGE(OpenMP)
IF(OPENMP_FOUND)
    MESSAGE(STATUS "OpenMP found")
    SET (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
    SET (CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${OpenMP_EXE_LINKER_FLAGS}")
ELSE()
    MESSAGE(STATUS "OpenMP not found")
ENDIF()

IF(WIN32)
	#mpi.h
	FIND_PATH(MPI_INCLUDE_DIRS NAMES "mpi.h")
	MESSAGE(STATUS "MPI_INCLUDE_DIRS=" ${MPI_INCLUDE_DIRS})
	if(NOT MPI_INCLUDE_DIRS)
		MESSAGE(FATAL_ERROR "mpi.h not found!")
	ENDIF()
	INCLUDE_DIRECTORIES(${MPI_INCLUDE_DIRS})

	# libmsmpi.a
	FIND_LIBRARY(MPI_C_LIBRARIES libmsmpi.a)
	MESSAGE(STATUS "MPI_C_LIBRARIES=" ${MPI_C_LIBRARIES})
	IF(NOT MPI_C_LIBRARIES)
		MESSAGE(FATAL_ERROR "mpi library not found!")
	ENDIF()

ELSE()
	FIND_PACKAGE(MPI)
	IF(MPI_FOUND)
		MESSAGE(STATUS "MPI found")
		INCLUDE_DIRECTORIES(${MPI_INCLUDE_PATH})
	ELSE()
		MESSAGE(FATAL_ERROR "MPI not found")
	ENDIF()
ENDIF()

ADD_SUBDIRECTORY( srcs )
ENABLE_TESTING()

